cmake_minimum_required(VERSION 3.20)
project(ShaderLabPlayer VERSION 1.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# Platform check
if(NOT WIN32)
    message(FATAL_ERROR "ShaderLab currently supports Windows only")
endif()

option(SHADERLAB_ENABLE_DXC "Enable DXC shader compiler (requires dxcompiler.dll)" ON)
option(SHADERLAB_STATIC_RUNTIME "Link MSVC runtime statically" OFF)
option(SHADERLAB_USE_CRINKLER "Use Crinkler as the linker (self-contained builds)" OFF)
option(SHADERLAB_CRINKLER_TINYIMPORT "Enable Crinkler /TINYIMPORT optimization" ON)
option(SHADERLAB_RUNTIME_IMGUI "Enable ImGui debug overlay in runtime player" ON)
option(SHADERLAB_COMPACT_TRACK_DEBUG "Enable compact track runtime debug logs" OFF)
option(SHADERLAB_RUNTIME_DEBUG_LOG "Enable general runtime debug logs" OFF)
option(SHADERLAB_TINY_PLAYER "Enable ultra-minimal runtime profile (strips optional subsystems)" OFF)
option(SHADERLAB_BUILD_MICRO_PLAYER "Build ultra-minimal benchmark runtime target" OFF)
set(CRINKLER_PATH "" CACHE FILEPATH "Path to crinkler.exe")

if(MSVC AND SHADERLAB_STATIC_RUNTIME)
    set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>")
endif()

if(MSVC AND SHADERLAB_USE_CRINKLER)
    if(CRINKLER_PATH)
        if(EXISTS "${CRINKLER_PATH}")
            set(CMAKE_LINKER "${CRINKLER_PATH}" CACHE FILEPATH "Crinkler linker" FORCE)
        else()
            message(WARNING "CRINKLER_PATH does not exist: ${CRINKLER_PATH}")
        endif()
    endif()
endif()

# Third-party dependencies
add_subdirectory(third_party)

# Core modules
add_library(ShaderLabCore STATIC
    src/graphics/Device.cpp
    src/graphics/Swapchain.cpp
    src/graphics/CommandQueue.cpp
    src/graphics/PreviewRenderer.cpp
    src/shader/ShaderCompiler.cpp
    src/audio/BeatClock.cpp
    src/core/Serializer.cpp
    src/core/PackageManager.cpp
    include/ShaderLab/Graphics/Device.h
    include/ShaderLab/Graphics/Swapchain.h
    include/ShaderLab/Graphics/CommandQueue.h
    include/ShaderLab/Graphics/PreviewRenderer.h
    include/ShaderLab/Shader/ShaderCompiler.h
    include/ShaderLab/Audio/AudioSystem.h
    include/ShaderLab/Audio/BeatClock.h
    include/ShaderLab/Core/Serializer.h
    include/ShaderLab/Core/PackageManager.h
    include/ShaderLab/Core/ShaderLabData.h
)

if(NOT SHADERLAB_TINY_PLAYER)
    target_sources(ShaderLabCore PRIVATE
        src/audio/AudioSystem.cpp
    )
endif()

target_include_directories(ShaderLabCore PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/include
    ${CMAKE_CURRENT_SOURCE_DIR}/third_party/imgui
    ${CMAKE_CURRENT_SOURCE_DIR}/third_party/json/include
    ${CMAKE_CURRENT_SOURCE_DIR}/third_party/stb
)

if(NOT SHADERLAB_TINY_PLAYER)
    target_include_directories(ShaderLabCore PUBLIC
        ${CMAKE_CURRENT_SOURCE_DIR}/third_party/miniaudio
    )
endif()

# Link D3D12 and related Windows libraries
target_link_libraries(ShaderLabCore PUBLIC
    d3d12.lib
    dxgi.lib
    dxguid.lib
    d3dcompiler.lib
)

if(SHADERLAB_ENABLE_DXC)
    target_link_libraries(ShaderLabCore PUBLIC dxcompiler.lib)
endif()

# Runtime uses ImGui only when overlay is enabled
if(SHADERLAB_RUNTIME_IMGUI)
    target_link_libraries(ShaderLabCore PUBLIC imgui)
endif()

if(SHADERLAB_RUNTIME_IMGUI)
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_RUNTIME_IMGUI=1)
else()
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_RUNTIME_IMGUI=0)
endif()

if(SHADERLAB_COMPACT_TRACK_DEBUG)
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_COMPACT_TRACK_DEBUG=1)
else()
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_COMPACT_TRACK_DEBUG=0)
endif()

if(SHADERLAB_RUNTIME_DEBUG_LOG)
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_RUNTIME_DEBUG_LOG=1)
else()
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_RUNTIME_DEBUG_LOG=0)
endif()

if(SHADERLAB_TINY_PLAYER)
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_TINY_PLAYER=1)
else()
    target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_TINY_PLAYER=0)
endif()

# Compiler flags
if(MSVC)
    target_compile_options(ShaderLabCore PRIVATE
        /W4           # Warning level 4
        /WX-          # Don't treat warnings as errors (for now)
        /permissive-  # Standards conformance
        /Zc:__cplusplus
    )

    if(SHADERLAB_ENABLE_DXC)
        target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_ENABLE_DXC=1)
    else()
        target_compile_definitions(ShaderLabCore PUBLIC SHADERLAB_ENABLE_DXC=0)
    endif()
    
    # Force Release optimization usage even in RelWithDebInfo if desired,
    # but sticking to defaults is safer.
    target_compile_options(ShaderLabCore PRIVATE /O2)
    if(NOT SHADERLAB_USE_CRINKLER)
        target_compile_options(ShaderLabCore PRIVATE /GL)
    endif()

    if(SHADERLAB_TINY_PLAYER)
        target_compile_options(ShaderLabCore PRIVATE
            /Os
            /Oy
            /Oi
            /fp:fast
            /GS-
            /GR-
            /Gy
            /Gw
            /GF
            /Zc:threadSafeInit-
        )
    endif()
endif()

# Player Executable
add_executable(ShaderLabPlayer WIN32
    src/app/runtime/main_player.cpp
    src/app/runtime/DemoPlayer.cpp
    include/ShaderLab/App/DemoPlayer.h
)

target_link_libraries(ShaderLabPlayer PRIVATE ShaderLabCore)

target_include_directories(ShaderLabPlayer PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/include
)

if(MSVC AND SHADERLAB_TINY_PLAYER)
    if(SHADERLAB_USE_CRINKLER)
        target_link_options(ShaderLabPlayer PRIVATE
            /INCREMENTAL:NO
            /MANIFEST:NO
            /RELEASE
            /COMPMODE:SLOW
            /ORDERTRIES:80
            /UNALIGNCODE
        )
    else()
        target_link_options(ShaderLabPlayer PRIVATE
            /INCREMENTAL:NO
            /OPT:REF
            /OPT:ICF
            /LTCG
            /MANIFEST:NO
            /RELEASE
        )
    endif()
endif()

if(SHADERLAB_BUILD_MICRO_PLAYER)
    add_executable(ShaderLabMicroPlayer WIN32
        src/app/runtime/main_micro_player.cpp
    )

    target_include_directories(ShaderLabMicroPlayer PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/include
    )

    if(MSVC)
        target_compile_options(ShaderLabMicroPlayer PRIVATE
            /W4
            /WX-
            /Os
            /Oy
            /Oi
            /GS-
            /GR-
            /Gy
            /Gw
            /GF
            /Zc:threadSafeInit-
        )

        if(SHADERLAB_USE_CRINKLER)
            target_link_options(ShaderLabMicroPlayer PRIVATE
                /ENTRY:WinMainCRTStartup
                /INCREMENTAL:NO
                /MANIFEST:NO
                /RELEASE
                /COMPMODE:SLOW
                /ORDERTRIES:120
                /UNALIGNCODE
            )
            if(SHADERLAB_CRINKLER_TINYIMPORT)
                target_link_options(ShaderLabMicroPlayer PRIVATE /TINYIMPORT)
            endif()
        else()
            target_link_options(ShaderLabMicroPlayer PRIVATE
                /ENTRY:WinMainCRTStartup
                /INCREMENTAL:NO
                /OPT:REF
                /OPT:ICF
                /LTCG
                /MANIFEST:NO
                /RELEASE
            )
        endif()
    endif()
endif()

if(SHADERLAB_ENABLE_DXC)
    # Copy DXC DLLs to output directory
    # We assume the Distributor has placed the DLLs in the parent directory of this script (the app root)
    add_custom_command(TARGET ShaderLabPlayer POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "${CMAKE_CURRENT_SOURCE_DIR}/../dxcompiler.dll"
            $<TARGET_FILE_DIR:ShaderLabPlayer>
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            "${CMAKE_CURRENT_SOURCE_DIR}/../dxil.dll"
            $<TARGET_FILE_DIR:ShaderLabPlayer>
    )
endif()
